pkgname=anime-wm-git
pkgver=0.3.1.r551.bf0922a
pkgrel=1
pkgdesc="wlroots compositor - anime-wm"
arch=("x86_64")
url="https://github.com/dy-tea/awm"
license=("GPL-3.0")
depends=(
    wlroots-git
    libxkbcommon
    wayland
    wayland-protocols
    libinput
)
makedepends=(
    git
    meson
    ninja
)
optdepends=(
    'pixman: for software rendering'
    'xwayland-satellite: better support for xwayland'
    'slurp: taking screenshots'
    'grim: taking screenshots'
    'swappy: annotating screenshots'
)
provides=("wayland-compositor")
conflicts=(anime-wm)
source=(
    "git+https://github.com/dy-tea/awm.git"
)
noextract=()
b2sums=('SKIP')

pkgver(){
    pkgver="0.3.1"
    cd "awm"
    echo "${pkgver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build(){
    cd "awm"
    arch-meson --wrap-mode default setup
    ninja -C setup
}

package(){
    cd "awm"
    meson install -C setup --destdir "$pkgdir"
}
